This is a package for easily selecting notes via consult. It is largely drawn
from this [[https://github.com/minad/consult/wiki/hrm-notes][wiki entry]] on the consult github page.

It can be used on its own or in tandem with org-roam (see the minor-mode in
=consult-notes-org-roam.el=. 

Consult-notes applies annotations in the minibuffer via [[https://github.com/minad/marginalia][marginalia]] and actions
on candidates via [[https://github.com/oantolin/embark][embark]].

NOTE: This is still in a fluctuating state, the package may change in
potentially breaking ways, so please update with care!

#+ATTR_HTML: :width 85%
[[file:screenshots/notes-screenshot.png]]

* Installation
#+begin_src emacs-lisp
(use-package consult-notes
  :straight (:type git :host github :repo "mclear-tools/consult-notes")
  :commands (consult-notes
             consult-notes-search-all
             consult-notes-org-roam-find-node
             consult-notes-org-roam-find-node-relation)
  :config
  (setq consult-notes-sources '(consult-notes--data-dirs
                                consult-notes--org-roam-nodes
                                consult-notes--org-roam-refs))
  (setq consult-notes-datadirs) ;; set notes dir(s), see below
  (consult-notes-org-roam-mode)) ;; set org-roam integration
#+end_src

* Usage

Set notes directories via =consult-notes-sources-data-dirs=. This is a list of a title,
key (for narrowing), and directory path. For example:

#+begin_src emacs-lisp
(setq consult-notes-sources-data-dirs
      '(("Org"             ?o "~/Dropbox/org-files/")
        ("Org Refile"      ?R "~/Dropbox/Work/projects/notebook/org-refile/")))
#+end_src

To search all your notes with grep (or ripgrep if installed) you need to either
limit your searches to a single directory, or create a directory containing
aliases of all the files or directories that you have notes in that you'd like
to search. Consult will treat all the aliased files and dirs as if they were all
together in one dir and searches them quickly and easily. Set this via
=consult-notes-all-notes=. 

If you want to integrate consult-notes with org-roam be sure to call the
minor-mode integration via =(consult-notes-org-roam-mode)=. This provides some
useful functions to integrate search of org-roam nodes and of org-roam
refereces. It also the org-roam display interface look like that of the
consult-notes interface when used apart from consult (e.g. in calling =org-roam-node-find=). More functionality may be added in the future. 

* Embark support

If you use [[https://github.com/oantolin/embark][embark]] you can integrate consult-notes actions with embark like so: 

#+begin_src emacs-lisp
(defun consult-notes-open-dired (cand)
  "Open notes directory dired with point on file CAND."
  (interactive "fNote: ")
  ;; dired-jump is in dired-x.el but is moved to dired in Emacs 28
  (dired-jump nil cand))

(defun consult-notes-marked (cand)
  "Open a notes file CAND in Marked 2.
Marked 2 is a mac app that renders markdown."
  (interactive "fNote: ")
  (call-process-shell-command (format "open -a \"Marked 2\" \"%s\"" (expand-file-name cand))))

(defun consult-notes-grep (cand)
  "Run grep in directory of notes file CAND."
  (interactive "fNote: ")
  (consult-grep (file-name-directory cand)))

(embark-define-keymap consult-notes-map
                      "Keymap for Embark notes actions."
                      :parent embark-file-map
                      ("d" consult-notes-dired)
                      ("g" consult-notes-grep)
                      ;; ("h" consult-notes-org-headline)
                      ("m" consult-notes-marked))

(add-to-list 'embark-keymap-alist `(,consult-notes-category . consult-notes-map))

;; make embark-export use dired for notes
(setf (alist-get consult-notes-category embark-exporters-alist) #'embark-export-dired)
#+end_src

* Citar Support
If you use [[https://github.com/emacs-citar/citar][citar]] you can integrate support with org-roam as follows:

#+begin_src emacs-lisp
;; Search org-roam notes for citations (depends on citar)
(defun consult-notes-org-roam-cited (reference)
  "Return a list of notes that cite the REFERENCE."
  (interactive (list (citar-select-ref
                      :rebuild-cache current-prefix-arg
                      :filter (citar-has-note))))
  (let* ((ids
          (org-roam-db-query [:select * :from citations
                              :where (= cite-key $s1)]
                             (car reference)))
         (anodes
          (mapcar (lambda (id)
                    (org-roam-node-from-id (car id)))
                  ids))
         (template
          (org-roam-node--process-display-format org-roam-node-display-template))
         (bnodes
          (mapcar (lambda (node)
                    (org-roam-node-read--to-candidate node template)) anodes))
         (node (completing-read
                "Node: "
                (lambda (string pred action)
                  (if (eq action 'metadata)
                      `(metadata
                        ;; get title using annotation function
                        (annotation-function
                         . ,(lambda (title)
                              (funcall org-roam-node-annotation-function
                                       (get-text-property 0 'node title))))
                        (category . org-roam-node))
                    (complete-with-action action bnodes string pred)))))
         (fnode
          (cdr (assoc node bnodes))))
    (if ids
        ;; Open node in other window
        (org-roam-node-open fnode)
      (message "No notes cite this reference."))))
#+end_src
